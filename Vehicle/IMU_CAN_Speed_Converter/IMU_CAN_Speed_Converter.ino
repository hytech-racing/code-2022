#include <stdint.h>
#include <HyTech_FlexCAN_T4.h>

// CAN Variables
FlexCAN_T4<CAN1, RX_SIZE_256, TX_SIZE_16> CAN_IMU; //Pins to IMU are A8 and A9 (22 and 23), which is CAN1
FlexCAN_T4<CAN2, RX_SIZE_256, TX_SIZE_16> CAN_Vehicle; //Pins to the Vehicle CanBus are D0 and D1 (0 and 1), which is CAN2
CAN_message_t msg;
unsigned char len = 0;
unsigned char buf[8];

void swap_bytes(uint8_t *low_byte, uint8_t high_byte);

// Initialize LEDs
#define IMU_LED 5
#define VEHICLE_LED 6
#define IMU_LED_TICKS_LIMIT 1000
#define VEHICLE_LED_TICKS_LIMIT 1000
int IMU_LED_ticks = 0;
int Vehicle_LED_ticks = 0;

// Options
#define DEBUG (false)
#define ZERO (false)

void setup() {
  // Initialize both sets of CAN lines
  CAN_IMU.begin();
  CAN_IMU.setBaudRate(1000000); //IMU CAN speed is 1Mbps
  CAN_Vehicle.begin();
  CAN_Vehicle.setBaudRate(500000); //Vehicle CAN speed is 500kbps

  // Set LED pinmodes
  pinMode(IMU_LED, OUTPUT);
  pinMode(VEHICLE_LED, OUTPUT);

  // Zero IMU
  #if ZERO
  CAN_message_t zero_msg;
  zero_msg.id = 0x10;
  zero_msg.len = 8;
  uint8_t zero_buf[8] = {0x69, 0x6d, 0x75, 0x7a, 0x65, 0x72, 0x6f, 0x7a};
  for (int i = 0; i < 8; i++) {
    buf[i] = zero_buf[i];
  }
  CAN_IMU.write(zero_msg);  
  delay(6000); // delay 6 seconds to perform zeroing
  #endif
  
  
  #if DEBUG
  Serial.begin(9600);
  Serial.println("CAN INIT OK!");
  #endif
}

void loop() {
  // LEDs
  if (IMU_LED_ticks == IMU_LED_TICKS_LIMIT / 2) {
    digitalWrite(IMU_LED, HIGH);
  }
  if (IMU_LED_ticks == IMU_LED_TICKS_LIMIT) {
    digitalWrite(IMU_LED, LOW);
    IMU_LED_ticks = 0;
  }
  if (Vehicle_LED_ticks == VEHICLE_LED_TICKS_LIMIT / 2) {
    digitalWrite(VEHICLE_LED, HIGH);
  }
  if (Vehicle_LED_ticks == VEHICLE_LED_TICKS_LIMIT) {
    digitalWrite(VEHICLE_LED, LOW);
    Vehicle_LED_ticks = 0;
  }

  // Read and write CAN message
  if (CAN_IMU.read(msg)) {
    IMU_LED_ticks++;
    
    #if DEBUG
    
    // accelerometer
    if (msg.id == ID_IMU_ACCELEROMETER) {
      int16_t lat_accel = ((int16_t)(msg.buf[0]) << 8) | msg.buf[1];
      int16_t long_accel = ((int16_t)(msg.buf[2]) << 8) | msg.buf[3];
      int16_t vert_accel = ((int16_t)(msg.buf[4]) << 8) | msg.buf[5];
      
      Serial.println("-----------------------------");
      Serial.print("Lateral:\t");
      Serial.println(lat_accel / 1000. * 9.813);
      Serial.print("Longitudinal:\t");
      Serial.println(long_accel / 1000. * 9.813);
      Serial.print("Vertical:\t");
      Serial.println(vert_accel / 1000. * 9.813);
      Serial.println();
    } 

    // gyroscope
    if (msg.id == ID_IMU_GYROSCOPE) {
      // multiply by 360 to get degrees
      int16_t yaw = (((int16_t)(msg.buf[0]) << 8) | msg.buf[1]) * 360;
      int16_t pitch = (((int16_t)(msg.buf[2]) << 8) | msg.buf[3]) * 360;
      int16_t roll = (((int16_t)(msg.buf[4]) << 8) | msg.buf[5]) * 360;

      Serial.println("-----------------------------");
      Serial.print("Yaw:\t");
      Serial.println(yaw / 1000.);
      Serial.print("Pitch:\t");
      Serial.println(pitch / 1000.);
      Serial.print("Roll:\t");
      Serial.println(roll / 1000.);
      Serial.println();
    }
    #endif

    swap_bytes(&msg.buf[1], &msg.buf[0]);
    swap_bytes(&msg.buf[3], &msg.buf[2]);
    swap_bytes(&msg.buf[5], &msg.buf[4]);

    CAN_Vehicle.write(msg);
    Vehicle_LED_ticks++;
  }
}

void swap_bytes(uint8_t *low_byte, uint8_t *high_byte) {
  uint8_t temp = *low_byte;
  *low_byte = *high_byte;
  *high_byte = temp;
}
